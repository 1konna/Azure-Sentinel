// Name: Exchange AuditLog Disabled
// Description: This will help you determine if excahnge audit logging is disabled which may be an attempt
// to evade detection or avoid other defenses by an adversary. 
//
// https://docs.microsoft.com/en-us/Exchange/policy-and-compliance/mailbox-audit-logging/enable-or-disable?view=exchserver-2019#enable-or-disable-mailbox-audit-logging
//
//Id: 194dd92e-d6e7-4249-85a5-273350a7f5ce
//
// Severity: Medium
//
// QueryFrequency: 1d
//
// QueryPeriod: 1d
//
// AlertTriggerOperator: gt
//
// AlertTriggerThreshold: 0
//
// DataSource: #OfficeActivity
//
// Tactics:  #DefenseEvasion
//
let timeframe = 1d;
OfficeActivity
| where TimeGenerated >= ago(timeframe)
| where UserType in ("Admin","DcAdmin") 
// Only admin or global-admin can disable audit logging
| where Operation == "Set-AdminAuditLogConfig" 
| extend AdminAuditLogEnabledValue= 
  tostring(parse_json(tostring(parse_json(tostring(array_slice(parse_json(Parameters),3,3)))[0])).Value)
| where AdminAuditLogEnabledValue == "False" 
| summarize min(TimeGenerated), max(TimeGenerated), OperationCount = count()
  by Operation, UserType, UserId, ResultStatus, Parameters, AdminAuditLogEnabledValue