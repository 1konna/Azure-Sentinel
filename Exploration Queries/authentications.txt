//Shows authentication volume by user agent and IP address. 
//Tracking via user agent is one way to differentiate between types of connecting device. 
//In homogeneous enterprise environments the user agent associated with an attacker device may stand out as unusual. 
//Examining authentications by new devices or originating from new IPs is a potential avenue to discover unauthorized access. 
//Similarly new device types (user agents) on a known IP address is potentially suspect.
// Office - authentications by UA & IP (likely only manageable for small tenants)
// Tags: #Discovery #LateralMovement #Collection
OfficeActivity
| where RecordType in ("AzureActiveDirectoryAccountLogon", "AzureActiveDirectoryStsLogon")
| where Operation startswith "UserLoggedIn"
| extend UserAgent = extractjson("$[0].Value", ExtendedProperties, typeof(string))
| project Operation, UserId, TimeGenerated , UserAgent, ClientIP 
| summarize userAgentCount=count() by UserAgent, ClientIP
| sort by userAgentCount desc;

//Shows authentication volume by user agent. 
//Tracking via user agent is one way to differentiate between types of connecting device. 
//In homogeneous enterprise environments the user agent associated with an attacker device may stand out as unusual. 
//Examining authentications by new devices is a potential avenue to discover unauthorized access.
// Tags: #Discovery #LateralMovement #Collection
OfficeActivity
| where RecordType in ("AzureActiveDirectoryAccountLogon", "AzureActiveDirectoryStsLogon")
| where Operation startswith "UserLoggedIn"
| extend UserAgent = extractjson("$[0].Value", ExtendedProperties, typeof(string))
| extend machineIndex=indexof(UserAgent, "machine_id") 
| extend UserAgent = substring(UserAgent, 0, machineIndex) 
| project Operation, UserId, TimeGenerated , UserAgent
| summarize userAgentCount=count() by UserAgent
| sort by userAgentCount desc;
