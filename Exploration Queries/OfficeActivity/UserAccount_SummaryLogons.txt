// Name: Office Activity Account logons 
// Description: Summary of IP addresses for logins based on Office Activity data.
//
// Entity: Account
// Input: Compromised Account
// Output: IP address
//
// QueryPeriod:  +-6h
//
// Data Source: #OfficeActivity
//
// Techniques: #Persistence, #Discovery, #LateralMovement, #Collection
//
let GetAllIPbyAccount = (suspiciousEventTime:datetime, v_Account:string){
let v_StartTime = suspiciousEventTime-6d;
let v_EndTime = suspiciousEventTime+1d;
OfficeActivity 
| where TimeGenerated between (v_StartTime .. v_EndTime)
| where UserId contains v_Account
| summarize min(TimeGenerated), max(TimeGenerated) by UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus
| project min_TimeGenerated, max_TimeGenerated, UserId, ClientIP, UserType, Operation, OfficeWorkload, ResultStatus
| top 10 by min_TimeGenerated asc nulls last 
};
// change datetime value and <username> value below
GetAllIPbyAccount (datetime('2019-01-30T10:36:07Z'), "<username>")