// Name: Alerts With This Process
// Description: Any Alerts that fired on any host with this same process in the range of +-1d
//
// Entity: Process
// Input: ProcessName
// Output: Alerts
//
// QueryPeriod: +-1d, change as needed
//
// Data Source: SecurityAlert
//
// Techniques: #Execution, #Persistence, #Discovery, #Lateral Movement, #Collection
//
let GetAllAlertsWithProcess = (suspiciousEventTime:datetime, qProcess:string){
let qStartTime = suspiciousEventTime-1d;
let qEndTime = suspiciousEventTime+1d;
SecurityAlert
| where TimeGenerated between (qStartTime .. qEndTime)
| extend Computer = toupper(parse_json(ExtendedProperties).["Compromised Host"])
| extend ProcessName = parse_json(ExtendedProperties).["Suspicious Process"]
| where ProcessName has qProcess
| project TimeGenerated, AlertName, Computer
| top 10 by TimeGenerated desc nulls last 
};
// change datetime value and <processname> value below
GetAllAlertsWithProcess(datetime('2019-01-18T10:36:07Z'), "<processname>")