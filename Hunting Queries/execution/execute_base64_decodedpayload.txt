// Process executed from binary hidden in Base64 encoded file.  Encoding malicious software is a technique to obfuscate files from detection.
//   The first ProcessCommandLine component is looking for Python decoding base64
//   The second ProcesssCommandLine component is looking for the Bash/sh commandline base64 decoding tool
//   The third one is looking for Ruby decoding base64
// Tags: #Initial Access, #Execution, #Defense Evasion
let ProcessCreationEvents=() {
let processEvents=SecurityEvent
| where EventID==4688
| project  EventTime=TimeGenerated, ComputerName=Computer,AccountName=SubjectUserName,        AccountDomain=SubjectDomainName,
  FileName=tostring(split(NewProcessName, '\\')[-1]), // convert SecurityEvents raw schema to get FileName & CommandLine 
ProcessCommandLine = CommandLine, 
FolderPath = "",
InitiatingProcessFileName=ParentProcessName,InitiatingProcessCommandLine="",InitiatingProcessParentFileName="";
processEvents ;
};
ProcessCreationEvents 
| where EventTime > ago(14d) 
| where ProcessCommandLine contains ".decode('base64')"
        or ProcessCommandLine contains "base64 --decode"
        or ProcessCommandLine contains ".decode64(" 
| project EventTime , ComputerName , FileName , ProcessCommandLine , InitiatingProcessCommandLine 
| top 100 by EventTime 


