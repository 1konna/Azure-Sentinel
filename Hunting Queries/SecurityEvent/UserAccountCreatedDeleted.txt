// Name: User Account Created and Deleted within 10mins
// Description: User account created and then deleted within 10 minutes across last 30 days
//
// Id: 6135a90e-ba30-4f36-9b6a-3a350050704b
//
// QueryPeriod: -30d
//
// Data Source: #SecurityEvent
//
// Techniques: #Persistence, #Discovery, #LateralMovement, #Collection
//
// TimeFrame is the number of lookback days, default is last 30 days
let timeframe = 30d;
// TimeDelta is the difference between when the account was created and when it was deleted, default is set to 10min or less
let timedelta = 10m;
SecurityEvent 
| where TimeGenerated > ago(timeframe) 
// A user account was created
| where EventID == "4720"
| where AccountType == "User"
| project TimeGenerated, EventID, Activity, Computer, TargetUserName, UserPrincipalName, SubjectUserName, TargetSid, SubjectUserSid 
| join kind= inner (
   SecurityEvent
   | where TimeGenerated > ago(timeframe) 
   // A user account was deleted 
   | where EventID == "4726" 
| where AccountType == "User"
| project TimeGenerated, EventID, Activity, Computer, TargetUserName, UserPrincipalName, SubjectUserName, TargetSid, SubjectUserSid 
) on Computer, TargetUserName
| where TimeGenerated < TimeGenerated1
| extend TimeDelta = TimeGenerated1 - TimeGenerated
| where TimeDelta <= timedelta
| project TimeDelta, StartTimeUtc = TimeGenerated, CreateEventID = EventID, Computer, TargetUserName, UserPrincipalName, AccountUsedToCreate = SubjectUserName, EndTimeUtc = TimeGenerated1, DeleteEventID = EventID1, AccountUsedToDelete = SubjectUserName1